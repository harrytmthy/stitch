/*
 * Copyright 2025 Harry Timothy Tumalewa
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    `kotlin-dsl`
}

group = "com.harrytmthy.stitch.buildlogic"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
}

dependencies {
    compileOnly(libs.android.gradle)
    compileOnly(libs.dokka.gradle)
    compileOnly(libs.kotlin.gradle)
    compileOnly(libs.maven.publish)
}

tasks {
    validatePlugins {
        enableStricterValidation = true
        failOnWarning = true
    }
}

gradlePlugin {
    plugins {
        register("stitchAndroidLibrary") {
            id = libs.plugins.stitch.android.library.get().pluginId
            implementationClass = "AndroidLibraryConventionPlugin"
        }
        register("stitchJvm") {
            id = libs.plugins.stitch.jvm.get().pluginId
            implementationClass = "JvmConventionPlugin"
        }
        register("stitchPublishing") {
            id = libs.plugins.stitch.publishing.get().pluginId
            implementationClass = "PublishingConventionPlugin"
        }
    }
}